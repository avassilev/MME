{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "30d8907e",
   "metadata": {},
   "source": [
    "# Discrete-time economic models"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "02af00aa",
   "metadata": {},
   "source": [
    "## The capital accumulation equation\n",
    "\n",
    "Capital accumulation in discrete time $t=0,1,2,\\ldots$ can be described by the following difference equation:\n",
    "\n",
    "$$K_{t+1} = (1-\\delta)K_t + I_t, \\text{ given an initial } K_0 \\geq 0.$$\n",
    "\n",
    "Here $K_t$ is the stock of physical capital at time $t$, $\\delta \\in (0,1)$ is the rate of depreciation and $I_t \\geq 0$ is investment at time $t$. In the simplest case investment is exogenously given."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "198ff0be",
   "metadata": {},
   "outputs": [],
   "source": [
    "K0 = 50 # initial capital stock\n",
    "δ = 0.05 # depreciation rate\n",
    "Inv = [2,3,3,4,1,2,0,2] # investment path\n",
    "\n",
    "K = zeros(size(Inv)[1] + 1)\n",
    "\n",
    "K[1] = K0\n",
    "\n",
    "for t in eachindex(Inv)\n",
    "    K[t+1] = (1-δ)*K[t] + Inv[t]\n",
    "end\n",
    "\n",
    "K"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cfccfbbe",
   "metadata": {},
   "source": [
    "# Scalar first-order difference equations -- some information\n",
    "\n",
    "A scalar first-order difference equation is given by\n",
    "\n",
    "$$x_{t+1} = f(x_t) \\qquad \\qquad (\\clubsuit)$$ \n",
    "\n",
    "for a given function $f: \\mathbb{R} \\rightarrow \\mathbb{R}$. A solution to this equation is a sequence $\\{x_t\\}_{t=0}^{\\infty}$ which satisfies the equation for any $t \\geq 0$. Sometimes we write $\\{x_t\\}$ for brevity if the limits are clear. It is also possible to write such an equation in the form $x_t = f(x_{t-1})$.\n",
    "\n",
    "### Initial value problems\n",
    "\n",
    "The problem of computing a sequence $x_0, x_1, x_2,\\ldots $ satisfying the above equation for a given initial value $x_0$ is called an *initial value problem*.\n",
    "\n",
    "Such a solution can be computed by iteration:\n",
    "\n",
    "$$x_1 = f(x_0), \\quad x_2 = f(x_1) = f(f(x_0)), \\quad x_3 = f(x_2) = f(f(f(x_0))), \\quad \\ldots $$\n",
    "\n",
    "### Steady state\n",
    "\n",
    "A value $x^*$ such that $x^* = f(x^*)$ is called a *steady state*, *stationary point* or *equilibrium* of equation $(\\clubsuit)$. Clearly, the sequence $x^*, x^*, \\ldots$ is a solution to the equation.\n",
    "\n",
    "### Stability\n",
    "\n",
    "Let $x^*$ be a stationary point. If, for every $\\epsilon > 0 $, there exists $\\delta>0$ such that \n",
    "$$|x_0-x^*| < \\delta \\quad \\Rightarrow \\quad |x_t - x^*| < \\epsilon, ~ \\forall t>0,$$ then $x^*$ is called *stable*.\n",
    "\n",
    "A point $x^*$ that is not stable is called *unstable*.\n",
    "\n",
    "If a point $x^*$ is stable and there exists $\\eta > 0$ such that a solution $\\{x_t\\}$ to equation $(\\clubsuit)$ satisfies\n",
    "$$\\lim_{t \\rightarrow \\infty}x_t = x^* $$ for any $x_0 ~:~ |x_0-x^*|<\\eta$, the point $x^*$ is called *asymptotically stable*. If $\\eta = +\\infty$, then $x^*$ is called *globally asymptotically stable*.\n",
    "\n",
    "### A stability result\n",
    "\n",
    "If $x^*$ is a stationary point of equation $(\\clubsuit)$, the function $f(\\cdot)$ is continuously differentiable at $x^*$ and \n",
    "$$|f'(x^*)| < 1,$$\n",
    "then $x^*$ is asymptotically stable.\n",
    "\n",
    "If $$|f'(x^*)| > 1,$$\n",
    "then $x^*$ is unstable."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b36ba625",
   "metadata": {
    "tags": []
   },
   "source": [
    "## Example: a first-order linear equation\n",
    "\n",
    "Consider the equation \n",
    "\n",
    "$$x_{t+1} = a x_t + b$$ with $x_0$ -- given.\n",
    "\n",
    "Try:\n",
    "- Setting $a=0.9$\n",
    "- Setting $a=-0.9$\n",
    "- Setting $a=1$\n",
    "- Setting $a=-1$\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "77afa625",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Plots\n",
    "using LaTeXStrings\n",
    "\n",
    "x0 = 2\n",
    "a = -1\n",
    "b = 1\n",
    "\n",
    "T = 50\n",
    "\n",
    "x = zeros(T)\n",
    "\n",
    "x[1] = x0\n",
    "for t in eachindex(x)\n",
    "    if t == T\n",
    "        break\n",
    "    else\n",
    "        x[t+1] = a*x[t]+b\n",
    "    end\n",
    "end\n",
    "\n",
    "bar(0:(T-1),x, xlabel = L\"t\", ylabel = L\"x_t\", legend = false)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "98895d6e",
   "metadata": {},
   "source": [
    "# The Harrod-Domar Growth Model\n",
    "\n",
    "- We work in discrete time and in the case of a closed economy\n",
    "- Income (output) is denoted by $Y_t$ and saving is denoted by $S_t$\n",
    "- Saving is assumed to be a constant share of income:\n",
    "$$ S_t = s Y_t, \\quad s \\in (0,1) $$\n",
    "- Investment $I_t$ is assumed to depend on the change in output (accelerator principle):\n",
    "$$ I_t = v(Y_t - Y_{t-1}), \\quad v>0$$\n",
    "- Saving equals investment in a closed economy:\n",
    "$$S_t = I_t$$\n",
    "- We therefore have\n",
    "$$ s Y_t = v(Y_t - Y_{t-1}) \\Rightarrow Y_t = \\dfrac{v}{v-s}Y_{t-1}$$ \n",
    "- For a given initial value $Y_0$, recursive substitution yields\n",
    "$$Y_t = \\left( \\dfrac{v}{v-s} \\right)^t Y_0$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "267b5c03",
   "metadata": {
    "tags": []
   },
   "outputs": [],
   "source": [
    "using Plots\n",
    "using LaTeXStrings\n",
    "\n",
    "Y0 = 10\n",
    "s = 0.1\n",
    "v = 2\n",
    "\n",
    "T = 50\n",
    "\n",
    "Y = zeros(T)\n",
    "\n",
    "Y[1] = Y0\n",
    "for t in eachindex(Y)\n",
    "    if t == T\n",
    "        break\n",
    "    else\n",
    "        Y[t+1] = v/(v-s)*Y[t]\n",
    "    end\n",
    "end\n",
    "\n",
    "plot(0:(T-1),Y, xlabel = L\"t\", ylabel = L\"Y_t\", legend = false)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "13e65593-40bc-42f5-a163-1f7a5672cba2",
   "metadata": {},
   "source": [
    "# The simple Samuelson-Hicks Model\n",
    "\n",
    "- We again work in discrete time and in the closed-economy case\n",
    "- Income (output) is denoted by $Y_t$, consumption is denoted by $C_t$, investment is $I_t$ and $A_t$ is autonomous (i.e. independent of income) investment.\n",
    "- Consumption is given by\n",
    "$$ C_t = \\gamma + c_1 Y_{t-1} + c_2 Y_{t-2}, \\quad c_1+c_2 \\in (0,1),~ c_1,c_2\\geq 0. $$\n",
    "- Variable investment $I_t$ is again described by an accelerator-type relationship:\n",
    "$$ I_t = v(Y_{t-1} - Y_{t-2}), \\quad v>0$$\n",
    "- The national income identity is:\n",
    "$$Y_t = C_t + I_t + A_t$$\n",
    "- Upon substitution, we get\n",
    "$$ Y_t = (v+c_1)Y_{t-1} - (v-c_2)Y_{t-2} + B_t,$$ \n",
    "where $B_t = A_t + \\gamma$\n",
    "- Defining $c = c_1+c_2$, we can write the previous equation as\n",
    "$$Y_t = c Y_{t-1} + (v-c_2)(Y_{t-1}-Y_{t-2}) + B_t,  \\qquad \\qquad (\\spadesuit)$$\n",
    "which shows that output depends on its previous-period level *and* its change in the previous period.\n",
    "- Now suppose that we have a particular solution $\\bar{Y}_t$ to $(\\spadesuit)$. Then we can define the deviation $y_t = Y_t - \\bar{Y}_t$ and obtain the equation in deviations:\n",
    "$$y_t = c y_{t-1} + w(y_{t-1}-y_{t-2}), \\qquad \\qquad (\\spadesuit \\spadesuit)$$\n",
    "where $w = v-c_2$.\n",
    "- If $\\bar{Y}_t$ is interpreted as a specific path of interest, e.g. an equilibrium trajectory, then $y_t$ models deviations from it (\"gaps\", deviations from equilibrium)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3cd78b62-a8bf-4d15-9141-f65b53109f69",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Plots\n",
    "using LaTeXStrings\n",
    "\n",
    "y0 = 1\n",
    "y1 = 1\n",
    "c1 = 0.2\n",
    "c2 = 0.3\n",
    "v = 0.6\n",
    "\n",
    "c = c1 + c2\n",
    "w = v - c2\n",
    "\n",
    "T = 50\n",
    "\n",
    "y = zeros(T)\n",
    "\n",
    "y[1] = y0\n",
    "y[2] = y1\n",
    "for t in eachindex(y)\n",
    "    if t == T-1\n",
    "        break\n",
    "    else\n",
    "        y[t+2] = c*y[t+1] + w*(y[t+1]-y[t])\n",
    "    end\n",
    "end\n",
    "\n",
    "plot(0:(T-1),y, xlabel = L\"t\", ylabel = L\"y_t\", legend = false)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "657677dd",
   "metadata": {},
   "outputs": [],
   "source": [
    "(1+sqrt(0.6))^2"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "fbfb72f5",
   "metadata": {},
   "source": [
    "### Exercise\n",
    "\n",
    "Try each of the following parameter combinations and check the type of solution obtained (oscillatory/non-oscillatory, damped/explosive):\n",
    "\n",
    "- `c1 = 0.2; c2 = 0.3; v = 0.1`\n",
    "- `c1 = 0.3; c2 = 0.2; v = 0.9`\n",
    "- `c1 = 0.3; c2 = 0.2; v = 1.2`\n",
    "- `c1 = 0.2; c2 = 0.2; v = 1.35`\n",
    "- `c1 = 0.2; c2 = 0.2; v = 3.5`\n",
    "\n",
    "**Hint**: If uncertain, try shortening or increasing the simulation horizon `T` and re-running the simulation."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cec34027",
   "metadata": {},
   "source": [
    "### Analytical aspects of the simple Samuelson-Hicks model\n",
    "\n",
    "- The preceding exercise demonstrates the model's capability to generate solutions that exhibit a diverse range of behaviours.\n",
    "- In order to classify the types of dynamics obtainable from the model, an analytical approach is required.\n",
    "- It can be shown that, defining $s = 1-c$, the types of solution as a function of $w$ are as follows:\n",
    "    | $w$ value                | Solution type               |\n",
    "|:-----------------------:|----------------------------|\n",
    "| $$w \\leq (1-\\sqrt{s})^2$$ | non-oscillatory, damped     |\n",
    "| $$(1-\\sqrt{s})^2 < w < 1$$ | oscillatory, damped         |\n",
    "| $$w=1$$                   | oscillatory, constant amplitude |\n",
    "| $$1 < w < (1+\\sqrt{s})^2$$ | oscillatory, explosive      |\n",
    "| $$(1+\\sqrt{s})^2 \\leq w$$ | non-oscillatory, explosive  |\n",
    "- The code cell below is virtually identical to the original code cell introducing the Samuelson-Hicks model, with the difference that $s$ and other key quantities from the table are computed and displayed. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b58d4d01",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Plots\n",
    "using LaTeXStrings\n",
    "\n",
    "y0 = 1\n",
    "y1 = 1\n",
    "c1 = 0.4\n",
    "c2 = 0.4\n",
    "v = 1.0\n",
    "\n",
    "c = c1 + c2\n",
    "w = v - c2\n",
    "s = 1 - c\n",
    "\n",
    "\n",
    "T = 20\n",
    "\n",
    "y = zeros(T)\n",
    "\n",
    "y[1] = y0\n",
    "y[2] = y1\n",
    "for t in eachindex(y)\n",
    "    if t == T-1\n",
    "        break\n",
    "    else\n",
    "        y[t+2] = c*y[t+1] + w*(y[t+1]-y[t])\n",
    "    end\n",
    "end\n",
    "\n",
    "println(\"s=$s, (1-√s)^2 = $((1-sqrt(s))^2), (1+√s)^2 = $((1+sqrt(s))^2),  w = $w\")\n",
    "\n",
    "plot(0:(T-1),y, xlabel = L\"t\", ylabel = L\"y_t\", legend = false)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b40439d1",
   "metadata": {},
   "source": [
    "# The dynamic IS-MP-PC model\n",
    "\n",
    "## The basic dynamic model\n",
    "\n",
    "- We work in the closed economy case. The model features 3 equations. All parameters below are assumed positive and signs are explicitly written in the respective equations.\n",
    "- Shocks referred to below are exogenous non-stochastic sequences.\n",
    "- The IS equation describes the demand side of the economy. Here $\\hat{y}_t$ is the output gap, $r_t$ is the real interest rate, $\\bar{r}$ is the equilibrium value of the real interest rate and $\\varepsilon_{1,t}$ is a (demand-side) shock. \n",
    "$$ \\hat{y}_t = -a(r_t - \\bar{r}) + \\varepsilon_{1,t} \\qquad \\qquad ~~~~(\\text{IS}) $$\n",
    "- The PC equation describes the supply side (PC stands for Phillips curve). Inflation is denoted by $\\pi_t$, equilibrium inflation is $\\bar{\\pi}$ and $\\varepsilon_{2,t}$ is a (supply-side) shock.\n",
    "$$ \\pi_t - \\bar{\\pi} = c (\\pi_{t-1} - \\bar{\\pi}) + b \\hat{y}_t + \\varepsilon_{2,t} \\quad  (\\text{PC}) $$\n",
    "- The central bank is assumed to control the real interest rate and react to deviations of output and inflation from equilibrium according to the following rule (MP equation):\n",
    "$$ r_t - \\bar{r} = d (\\pi_t - \\bar{\\pi}) + e \\hat{y}_t \\qquad \\qquad ~~(\\text{MP}) $$\n",
    "\n",
    "- It is convenient to introduce the notation $\\rho_t := r_t - \\bar{r}$ and $\\omega_t := \\pi_t - \\bar{\\pi}$.\n",
    "- It can then be shown that one obtains an equation for $\\omega_t$ of the form\n",
    "$$\\omega_t = \\gamma \\omega_{t-1} + \\varepsilon_t,$$\n",
    "where $$\\gamma := \\frac{c(1+ae)}{1+ae+bad}, \\qquad \\varepsilon_t := \\frac{1+ae}{1+ae+bad}\\left( \\frac{b}{1+ae}\\varepsilon_{1,t} + \\varepsilon_{2,t} \\right).$$\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "776034c9",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Plots\n",
    "using LaTeXStrings\n",
    "\n",
    "a = 0.5\n",
    "b = 1.0\n",
    "c = 0.3\n",
    "d = 1.1\n",
    "e = 1.1\n",
    "\n",
    "omega0 = 0.0 # or something different from 0 if we are starting away from equilibruim\n",
    "\n",
    "T = 20\n",
    "\n",
    "epsilon1 = zeros(T)\n",
    "epsilon2 = zeros(T)\n",
    "omega = zeros(T+1)\n",
    "\n",
    "# here one can specify different paths for the exogenous shocks\n",
    "epsilon1[1] = 0.01\n",
    "\n",
    "\n",
    "gamma = c*(1 + a*e)/(1 + a*e + b*a*d)\n",
    "epsilon = (1 + a*e)/(1 + a*e + b*a*d)*(b/(1 + a*e)*epsilon1 + epsilon2)\n",
    "\n",
    "for t in eachindex(omega)\n",
    "    if t == 1\n",
    "        omega[t] = omega0\n",
    "    else\n",
    "        omega[t] = gamma*omega[t-1]+epsilon[t-1] # note the change in timing of epsilon relative to the formulation\n",
    "    end\n",
    "end\n",
    "\n",
    "plot(0:T, omega, xlabel = L\"t\", ylabel = L\"\\omega_t\", legend = false)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1649924e",
   "metadata": {},
   "source": [
    "## A model with enriched dynamics\n",
    "\n",
    "- Consider a version of the model with the shocks (temporarily) eliminated.\n",
    "- We now have the following equations:\n",
    "$$ \\hat{y}_t = -a \\rho_{t-1} + f \\hat{y}_{t-1} \\qquad \\qquad ~~~~(\\text{IS-1}) $$\n",
    "$$ \\omega_t = c \\omega_{t-1} + b \\hat{y}_{t-1} \\qquad \\qquad ~~~~~~ (\\text{PC-1}) $$\n",
    "$$ \\rho_t = d \\omega_{t-1} + e \\hat{y}_{t-1} \\qquad \\qquad ~~~~~~(\\text{MP-1}) $$\n",
    "- Interpretation:\n",
    "  - The above specification introduces a number of delays in the response of the economy.\n",
    "  - The parameters $f$ and $c$ introduce a degree of inertia in the development of the economy.\n",
    "  - It is also assumed that interest rate deviations from equilibrium affect output with a lag of one period. The same holds true about the impact of the output gap on inflation, as well as the monetary policy response to output and inflation changes from equilibruim.\n",
    "- This model can be written compactly as a homogeneous system of difference equations in matrix form:\n",
    "\n",
    "$$\n",
    "\\begin{pmatrix}\n",
    "\\hat{y}_t \\\\\n",
    "\\omega_t  \\\\\n",
    "\\rho_t \\\\\n",
    "\\end{pmatrix}\n",
    "=\n",
    "\\begin{pmatrix}\n",
    "f & 0 & -a \\\\\n",
    "b & c & 0 \\\\\n",
    "e & d & 0 \\\\\n",
    "\\end{pmatrix}\n",
    "\\begin{pmatrix}\n",
    "\\hat{y}_{t-1} \\\\\n",
    "\\omega_{t-1} \\\\\n",
    "\\rho_{t-1} \\\\\n",
    "\\end{pmatrix}\n",
    "$$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a65a3010",
   "metadata": {},
   "source": [
    "### A note on homogeneous systems of difference equations\n",
    "\n",
    "Consider the system\n",
    "$$x_t = A x_{t-1}, \\qquad \\qquad (S)$$\n",
    "where $x_t \\in \\mathbb{R}^n$ and $A$ is an $n\\times n$ non-singular matrix. Starting from an initial condition $x_0$, the solution at time $t$ can be written as \n",
    "$$x_t = A^t x_0.$$\n",
    "\n",
    "If the matrix $I-A$ is invertible, then the only stationary point of (S) is the zero solution.\n",
    "\n",
    "A necessary and sufficient condition for the system of difference equations (S) to be globally asymptotically stable is that all eigenvalues of the matrix $A$ have moduli strictly less than 1."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "90e0718f",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Plots\n",
    "using LaTeXStrings\n",
    "using LinearAlgebra\n",
    "\n",
    "T = 30\n",
    "\n",
    "a = 0.5\n",
    "b = 0.5\n",
    "c = 0.3\n",
    "d = 0.5\n",
    "e = 0.5\n",
    "f = 0.6\n",
    "\n",
    "A = [f 0 -a;\n",
    "     b c 0;\n",
    "     e d 0]\n",
    "\n",
    "yhat0 = 0.01\n",
    "omega0 = 0.0\n",
    "rho0 = 0.0\n",
    "\n",
    "initial_state = [yhat0, omega0, rho0]\n",
    "\n",
    "println(\"Max of eigenvalues moduli: $(maximum(abs.(eigvals(A))))\")\n",
    "\n",
    "solution_path = initial_state\n",
    "\n",
    "for t in 1:T\n",
    "    state_at_t = A^t * initial_state\n",
    "    solution_path = hcat(solution_path, state_at_t)\n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "# Plotting the solutions\n",
    "labels = [L\"\\hat{y}_t\", L\"\\omega_t\", L\"\\rho_t\"]\n",
    "layout = @layout [pane1; pane2; pane3]\n",
    "\n",
    "p = plot(layout = layout)\n",
    "\n",
    "\n",
    "for i in 1:size(solution_path, 1)\n",
    "    series = solution_path[i, :]\n",
    "    plot!(p[i], 0:T, series, xlabel = L\"t\", ylabel = labels[i], legend = false)\n",
    "    hline!(p[i], [0], linecolor = :red, linewidth = 0.8, linestyle = :solid)\n",
    "end\n",
    "\n",
    "p"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.9.0",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
