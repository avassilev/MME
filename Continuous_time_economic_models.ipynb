{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "593d70db",
   "metadata": {},
   "source": [
    "# Programming differential equations in Julia\n",
    "\n",
    "Julia has a rich ecosystem for modelling based on differential equations. The [SciML](https://sciml.ai/) project provides a convenient entry point.\n",
    "\n",
    "The ecosystem provides different tools to define and solve differential equations, as well as explore the solutions obtained."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "74ab1c57",
   "metadata": {},
   "source": [
    "### Example 1\n",
    "\n",
    "Consider the equation $$\\frac{d}{dt}x(t) = \\alpha x(t).$$\n",
    "\n",
    "Given an initial condition $x(0) = x_0$, it can be verified that the solution to the initial value problem is given by $$x(t) = x_0 e^{\\alpha t}.$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "dd6eb20b",
   "metadata": {},
   "outputs": [],
   "source": [
    "using ModelingToolkit, DifferentialEquations, Plots\n",
    "\n",
    "@independent_variables t\n",
    "@variables x(t)=0.5 # specify symbolically the variables involved together with the initial condition\n",
    "@parameters α=0.2 # define parameters\n",
    "\n",
    "# the symbolic differentiation operator\n",
    "D = Differential(t) \n",
    "\n",
    "# define the equation(s)\n",
    "eqs = [D(x) ~ α*x]\n",
    "\n",
    "# construct a symbolic system\n",
    "@named sys = ODESystem(eqs, t)\n",
    "\n",
    "simpsys = structural_simplify(sys)\n",
    "\n",
    "# define the timespan of the simulation\n",
    "tspan = (0.0, 10.0) \n",
    "\n",
    "# convert to ODE problem from symbolic form\n",
    "prob = ODEProblem(simpsys, [], tspan) \n",
    "\n",
    "# Solve the ODE problem\n",
    "sol = solve(prob) \n",
    "\n",
    "# play around with the result\n",
    "plot(sol, title = \"Linear ODE\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "60ad5680",
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "sol(0) # value of solution at 0"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9c5853fa",
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "sol(10) # value of solution at 10"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a4b0ee17",
   "metadata": {},
   "outputs": [],
   "source": [
    "sol # note the form of u (a vector of vectors), as well as the tabulation grid"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ffe48742",
   "metadata": {},
   "outputs": [],
   "source": [
    "# here is how to tabulate over a pre-specified grid\n",
    "sol = solve(prob, saveat = range(tspan[1], stop = tspan[2], length = 100)) "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e170ed46",
   "metadata": {},
   "source": [
    "A simpler (and perhaps slightly outdated) way of accomplishing the same."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3d76f2e4",
   "metadata": {},
   "outputs": [],
   "source": [
    "using DifferentialEquations, Plots\n",
    "\n",
    "function lin_ode!(du, u, p, t)\n",
    "    du[1] = p[1] * u[1]\n",
    "end\n",
    "\n",
    "u0 = [0.5]\n",
    "p = [0.2]\n",
    "tspan = (0.0, 10.0)  # Time interval for solution\n",
    "\n",
    "prob1 = ODEProblem(lin_ode!, u0, tspan, p)\n",
    "sol1 = solve(prob1, Tsit5()) # note that the solver has been explicitly specified here\n",
    "\n",
    "plot(sol1)\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e685fd1e",
   "metadata": {},
   "outputs": [],
   "source": [
    "sol1"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c084c740",
   "metadata": {},
   "source": [
    "### Example 2 (the Lotka-Volterra predator-prey model, from the documentation)\n",
    "\n",
    "We consider the interaction between a prey species with population density $x(t)$ and a predator species with population density $y(t)$. The parameter $\\alpha$ is the birth rate of the prey species, $\\gamma$ is the death rate of the predator species and the parameters $\\beta$ and $\\delta$ describe the effects of interaction between species.\n",
    "\n",
    "$$ \\frac{dx}{dt} = \\alpha x - \\beta x y $$\n",
    "$$ \\frac{dy}{dt} = -\\gamma y + \\delta x y $$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4c52cc9d",
   "metadata": {},
   "outputs": [],
   "source": [
    "using ModelingToolkit, DifferentialEquations, Plots\n",
    "\n",
    "@independent_variables t\n",
    "# Define our state variables: state(t) = initial condition\n",
    "@variables x(t)=1 y(t)=1 z(t)=2\n",
    "\n",
    "# Define our parameters\n",
    "@parameters α=1.5 β=1.0 γ=3.0 δ=1.0\n",
    "\n",
    "# Define our differential: takes the derivative with respect to `t`\n",
    "D = Differential(t)\n",
    "\n",
    "# Define the differential equations\n",
    "# Note the last element, which is an algebraic equation counting the total number of animals\n",
    "eqs = [D(x) ~ α * x - β * x * y\n",
    "       D(y) ~ -γ * y + δ * x * y\n",
    "       z ~ x + y] \n",
    "\n",
    "# Bring these pieces together into an ODESystem with independent variable t\n",
    "@named sys = ODESystem(eqs, t)\n",
    "\n",
    "# Symbolically Simplify the System (removes the algebraic equation)\n",
    "simpsys = structural_simplify(sys)\n",
    "\n",
    "# Convert from a symbolic to a numerical problem to simulate\n",
    "tspan = (0.0, 10.0)\n",
    "prob = ODEProblem(simpsys, [], tspan)\n",
    "\n",
    "# Solve the ODE\n",
    "sol = solve(prob)\n",
    "\n",
    "# Plot the solution\n",
    "p1 = plot(sol, title = \"Rabbits vs Wolves\")\n",
    "p2 = plot(sol, idxs = z, title = \"Total Animals\")\n",
    "\n",
    "plot(p1, p2, layout = (2, 1))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a343dcd0",
   "metadata": {},
   "source": [
    "# The augmented Solow growth model\n",
    "\n",
    "### Model setup\n",
    "\n",
    "- Closed economy, continuous time\n",
    "- Main variables: output $Y$, capital $K$, labour $L$ and technological progress $A$\n",
    "- Labour-augmenting production technology:\n",
    "  $$Y(t) = F(K(t),A(t)L(t))$$\n",
    "- Assumptions about the production function:\n",
    "    - $F(0,0)=0$\n",
    "    - Positive marginal products, law of diminishing returns\n",
    "    $$F'_{x_i} > 0, \\quad F''_{x_i x_i} < 0$$\n",
    "    - Constant returns to scale\n",
    "    $$F(cK,cAL) = cF(K,AL),~\\forall c \\geq 0$$\n",
    "    - Inada conditions\n",
    "    $$\\lim_{x_i \\rightarrow 0}F'_{x_i} = +\\infty,\\quad \\lim_{x_i \\rightarrow +\\infty}F'_{x_i} = 0$$\n",
    "- Variables per unit of *effective* labour:\n",
    "    $$c=\\frac{C}{AL},~y=\\frac{Y}{AL},~k=\\frac{K}{AL}$$\n",
    "- Production function in intensive form\n",
    "  $$\\frac{1}{AL}F\\left(K,AL \\right) = F\\left( \\frac{K}{AL},1 \\right) =: f(k) \\qquad \\Rightarrow \\qquad y=f(k)$$\n",
    "- Properties of $f(k)$\n",
    "    - $f(0)=0$\n",
    "    - $f'(k)>0, \\quad f''(k)<0 $\n",
    "    - $\\lim_{k \\rightarrow 0}f'_{k} = +\\infty,\\quad \\lim_{k \\rightarrow +\\infty}f'_{k} = 0$\n",
    "\n",
    "\n",
    "**Example** (Cobb-Douglas production function): If $F(K,AL) = K^\\alpha (AL)^{1-\\alpha},~~ 0<\\alpha<1$, then $f(k)=k^\\alpha$.\n",
    "\n",
    "\n",
    "- Exogenous growth dynamics\n",
    "  $$\\dot{L}(t) = n L(t) \\qquad \\Rightarrow \\qquad L(t) = L(0)e^{nt}$$\n",
    "  $$\\dot{A}(t) = g A(t) \\qquad \\Rightarrow \\qquad A(t) = A(0)e^{gt}$$\n",
    "  \n",
    "  Here $n$ is the constant population growth rate and $g$ is the constant technology growth rate.\n",
    "- Capital accumulation\n",
    "    $$\\dot{K}(t) = sY(t)-\\delta K(t),$$\n",
    "    where $s \\in (0,1)$ is the saving rate and $\\delta \\in (0,1)$ is the depreciation rate.\n",
    "    "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "74056e61",
   "metadata": {},
   "source": [
    "### The main equation\n",
    "\n",
    "\\begin{array}[lll] $\\dot{k}(t) & = &   \\dfrac{\\dot{K}(t)}{A(t)L(t)} - \\dfrac{K(t)}{(A(t)L(t))^2}[A(t)\\dot{L}(t)+L(t)\\dot{A}(t)] \\\\\n",
    "{} & = & \\dfrac{\\dot{K}(t)}{A(t)L(t)} - \\dfrac{K(t)}{A(t)L(t)} \\dfrac{\\dot{L}(t)}{L(t)} - \\dfrac{K(t)}{A(t)L(t)} \\dfrac{\\dot{A}(t)}{A(t)} \\\\ {} & = &  \\dfrac{sY(t) - \\delta K(t)}{A(t)L(t)} - k(t)n - k(t) g \\\\ {} & = &  \\dfrac{sY(t)}{A(t)L(t)} - \\delta k(t) - n k(t) - g k(t) \\\\ {} & = & s f(k(t)) - (n+g+\\delta)k(t).\n",
    "\\end{array}"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ab1b9627",
   "metadata": {},
   "source": [
    "### Steady state\n",
    "\n",
    "$$\\dot{k}(t) = 0 \\qquad \\Rightarrow \\qquad s f(k^*) = (n+g+\\delta)k^* $$\n",
    "\n",
    "Clearly we have the trivial steady state $k^* = 0$. It can be shown that there is one more steady state $k^* > 0$."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "bf5693be",
   "metadata": {},
   "source": [
    "### Coding the Solow model"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b8db0a50",
   "metadata": {},
   "outputs": [],
   "source": [
    "using ModelingToolkit, DifferentialEquations, Plots\n",
    "\n",
    "@independent_variables t\n",
    "@variables k(t)=1.0 \n",
    "@parameters α=0.6 δ=0.08 n=0.03 g=0.02 s=0.2\n",
    "\n",
    "f(k) = k^α\n",
    "\n",
    "D = Differential(t)\n",
    "\n",
    "\n",
    "eqs = [D(k) ~ s*f(k) - (n + g + δ)*k] \n",
    "\n",
    "@named sys = ODESystem(eqs, t);\n",
    "\n",
    "sys = structural_simplify(sys)\n",
    "\n",
    "tspan = (0.0, 100.0)\n",
    "prob = ODEProblem(sys, [], tspan)\n",
    "\n",
    "sol = solve(prob)\n",
    "\n",
    "plot(sol)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c9d95ca2",
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "# A hacky way to compute and display the steady state\n",
    "\n",
    "using ModelingToolkit, DifferentialEquations, Plots, Roots\n",
    "\n",
    "alpha = 0.6\n",
    "delta = 0.08\n",
    "nn = 0.03\n",
    "gg = 0.02\n",
    "ss = 0.2\n",
    "\n",
    "@independent_variables t\n",
    "@variables k(t)=10.0 \n",
    "@parameters α=alpha δ=delta n=nn g=gg s=ss\n",
    "\n",
    "f(k) = k^alpha\n",
    "\n",
    "D = Differential(t)\n",
    "\n",
    "\n",
    "eqs = [D(k) ~ s*f(k) - (n + g + δ)*k] \n",
    "\n",
    "@named sys = ODESystem(eqs, t);\n",
    "sys = structural_simplify(sys)\n",
    "\n",
    "function ODE_rhs(k, α = alpha, δ = delta, n = nn, g = gg, s = ss)\n",
    "    return s*f(k) - (n + g + δ)*k\n",
    "end\n",
    "\n",
    "steady_state = fzero(ODE_rhs, 0.0001, 1000)\n",
    "\n",
    "tspan = (0.0, 100.0)\n",
    "prob = ODEProblem(sys, [], tspan)\n",
    "\n",
    "sol = solve(prob)\n",
    "\n",
    "plot(sol)\n",
    "hline!([steady_state], linecolor = :red, linewidth = 0.8, linestyle = :solid, label=\"\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "72fa7867",
   "metadata": {},
   "source": [
    "### Some takeaways\n",
    "\n",
    "- The model has a steady state. It implies that in equilibrium absolute variables grow at a rate of population growth plus technology growth.\n",
    "- The model is globally asymptotically stable\n",
    "- Capital-poor economies can expect to grow and accumulate capital over time\n",
    "- It is possible to have a process of \"decapitalization\" in effective per capita terms if initial capital is too high"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "35ccbd14",
   "metadata": {},
   "source": [
    "# Cagan model with sluggish wages\n",
    "\n",
    "- The model was originally created to analyse periods of hyperinflation. It treats the closed economy case.\n",
    "- Notation (all variables in logs, i.e. $y = \\ln Y $ etc.)\n",
    "  - $y$ - output\n",
    "  - $n$ - employment\n",
    "  - $\\bar{n}$ - full employment (constant)\n",
    "  - $p$ - price level\n",
    "  - $\\bar{m}$ - nominal money stock (constant)\n",
    "  - $w$ - wage rate\n",
    "- Equations\n",
    "    $$\\begin{align}\n",
    "    & \\bar{m}-p(t) = \\alpha_1 y(t) - \\alpha_2 \\dot{p}(t), \\qquad \\alpha_1,\\alpha_2 > 0  \\tag{C-1} \\\\\n",
    "    & y(t) = c + (1-\\theta)n(t),\\qquad  0<\\theta<1 & \\tag{C-2} \\\\\n",
    "    & w(t)-p(t) = a - \\theta n(t) \\tag{C-3} \\\\\n",
    "    & \\dot{w}(t) = \\gamma (n(t) - \\bar{n}),\\qquad  \\gamma > 0  \\tag{C-4}\n",
    "    \\end{align}$$\n",
    "\n",
    "  - Equation (C-1) describes money market equilibrium. \n",
    "  - Equation (C-2) is the Cobb-Douglas production function\n",
    "  - Equation (C-3) gives the demand for labour (a standard \"marginal product equals price\" condition)\n",
    "  - Equation (C-4) is a Phillips curve\n",
    "  \n",
    " \n",
    "\n",
    "\n",
    "\n",
    "\n",
    "  "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "114f4c5f",
   "metadata": {},
   "outputs": [],
   "source": [
    "using ModelingToolkit, DifferentialEquations, Plots\n",
    "\n",
    "@parameters mbar=1 α₁=0.5 α₂=0.5 c=10 θ=0.2 a=1 nbar=2 γ=2\n",
    "n0=0.9; w0=1; \n",
    "y0=c+(1-θ)*n0; p0=w0-a+θ*n0;\n",
    "\n",
    "@independent_variables t\n",
    "@variables y(t)=y0 p(t)=p0 n(t)=n0 w(t)=w0\n",
    "\n",
    "D = Differential(t)\n",
    "\n",
    "eqs = [mbar - p ~ α₁*y - α₂*D(p)\n",
    "       y ~ c + (1-θ)*n\n",
    "       w-p ~ a-θ*n\n",
    "       D(w) ~ γ*(n-nbar)]\n",
    "\n",
    "\n",
    "@named sys = ODESystem(eqs, t)\n",
    "\n",
    "simpsys = structural_simplify(sys)\n",
    "\n",
    "\n",
    "tspan = (0.0, .03)\n",
    "prob = ODEProblem(simpsys, [], tspan)\n",
    "\n",
    "sol = solve(prob)\n",
    "\n",
    "p1 = plot(sol, idxs = y, title = \"Output\")\n",
    "p2 = plot(sol, idxs = n, title = \"Employment\")\n",
    "p3 = plot(sol, idxs = p, title = \"Price level\")\n",
    "p4 = plot(sol, idxs = w, title = \"Wage rate\")\n",
    "\n",
    "plot(p1, p2, p3, p4, layout = (2, 2))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8a83dd93",
   "metadata": {},
   "source": [
    "# A New Keynesian model in continuous time\n",
    "\n",
    "- Based on Ivan Werning, \"Managing a Liquidity Trap: Monetary and Fiscal Policy\", NBER Working Paper No. 17344, [http://www.nber.org/papers/w17344](http://www.nber.org/papers/w17344)\n",
    "- Notation\n",
    "    - $x(t)$ - output gap\n",
    "    - $\\pi(t)$ - inflation rate\n",
    "    - $r(t)$ - natural interest rate (exogenous)\n",
    "    - $i(t)$ - nominal interest rate\n",
    "- Model (log-linearized around zero inflation)\n",
    "$$\\begin{align}\n",
    "    & \\dot{x}(t) = \\sigma^{-1}(i(t)-r(t)-\\pi(t)), \\quad \\sigma>0 \\tag{NK-1} \\\\\n",
    "    & \\dot{\\pi}(t) = \\rho \\pi(t) - \\kappa x(t), \\quad \\rho, \\kappa>0  \\tag{NK-2}  \\\\\n",
    "    & i(t) \\geq 0  \\tag{NK-3} \\\\\n",
    "    \\end{align}$$\n",
    "- Equation (NK-1) is the IS curve derived from an Euler equation\n",
    "- Equation (NK-2) is the New Keynesian Phillips curve\n",
    "- Parameters\n",
    "    - The parameter $\\rho$ is the time discount rate\n",
    "    - The parameter $\\sigma^{-1}$ is the intertemporal elasticity of substitution\n",
    "    - The parameter $\\kappa$ measures price stickiness. When $\\kappa \\rightarrow +\\infty$ we have perfectly flexible prices.\n",
    "- Assume we evaluate macroeconomic outcomes using the quadratic loss function\n",
    "  $$L := \\frac{1}{2}\\int_0^\\infty e^{-\\rho t} \\left( x(t)^2 + \\lambda \\pi(t)^2 \\right)\\, dt,$$ where $\\lambda > 0$ captures the relative weight placed on the inflation objective\n",
    "- It can be shown that if the constraint (NK-3) is **not** binding, then the optimal policy takes the form \n",
    "$$i^*(t)=r(t)+(1-\\kappa \\sigma \\lambda)\\pi(t)$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fb042b18",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Werning's optimal rule\n",
    "using ModelingToolkit, DifferentialEquations, Plots, LinearAlgebra\n",
    "\n",
    "σ = 0.99\n",
    "ρ = 0.02\n",
    "κ = 0.5\n",
    "λ = 1\n",
    "\n",
    "A = [ 0   -κ*λ  \n",
    "     -κ      ρ]\n",
    "println(eigvals(A)) # both real parts should be negative to ensure asymptotic stability\n",
    "\n",
    "\n",
    "r(t) = 0.03\n",
    "i_star(t,infl) = r(t) + (1-κ*σ*λ)*infl\n",
    "\n",
    "\n",
    "@parameters σ=σ ρ=ρ κ=κ λ=λ\n",
    "\n",
    "@independent_variables t\n",
    "@variables x(t)=0.01 Π(t)=0.01 # inflation is Π instead of π to avoid variable name clashes\n",
    "\n",
    "D = Differential(t)\n",
    "\n",
    "eqs = [ D(x) ~ σ^(-1)*(i_star(t,Π) - r(t) - Π)\n",
    "        D(Π) ~ ρ*Π - κ*x ]\n",
    "\n",
    "\n",
    "@named sys = ODESystem(eqs, t)\n",
    "\n",
    "sys = structural_simplify(sys)\n",
    "\n",
    "tspan = (0.0, 25.0)\n",
    "prob = ODEProblem(sys, [], tspan)\n",
    "\n",
    "sol = solve(prob)\n",
    "\n",
    "p1 = plot(sol, idxs = x, title = \"Output gap\")\n",
    "p2 = plot(sol, idxs = Π, title = \"Inflation\")\n",
    "\n",
    "\n",
    "plot(p1, p2, layout = (2, 1))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "5b49ea77",
   "metadata": {},
   "source": [
    "- Let's consider a simple monetary policy rule of the form\n",
    "$$i^*(t) = r(t) + \\lambda_1 x(t) + \\lambda_2 \\pi(t)$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "317067aa",
   "metadata": {},
   "outputs": [],
   "source": [
    "# A simple monetary policy reaction function\n",
    "using ModelingToolkit, DifferentialEquations, Plots, LinearAlgebra\n",
    "\n",
    "σ = 0.99\n",
    "ρ = 0.02\n",
    "κ = 0.5\n",
    "λ₁ = -1\n",
    "λ₂ = 1.5\n",
    "\n",
    "\n",
    "A = [λ₁/σ  (λ₂-1)/σ\n",
    "     -κ     ρ]\n",
    "println(eigvals(A)) # both real parts should be negative to ensure asymptotic stability\n",
    "\n",
    "\n",
    "r(t) = 0.03\n",
    "i_star(t,og,infl) = r(t) + λ₁*og + λ₂*infl\n",
    "\n",
    "\n",
    "@parameters σ=σ ρ=ρ κ=κ \n",
    "\n",
    "@independent_variables t\n",
    "@variables x(t)=0.01 Π(t)=0.01 # inflation is Π instead of π to avoid variable name clashes\n",
    "\n",
    "D = Differential(t)\n",
    "\n",
    "eqs = [ D(x) ~ σ^(-1)*(i_star(t,x,Π) - r(t) - Π) \n",
    "        D(Π) ~ ρ*Π - κ*x ]\n",
    "\n",
    "@named sys = ODESystem(eqs, t)\n",
    "sys = structural_simplify(sys)\n",
    "\n",
    "tspan = (0.0, 25.0)\n",
    "prob = ODEProblem(sys, [], tspan)\n",
    "\n",
    "sol = solve(prob)\n",
    "\n",
    "p1 = plot(sol, idxs = x, title = \"Output gap\")\n",
    "p2 = plot(sol, idxs = Π, title = \"Inflation\")\n",
    "\n",
    "\n",
    "plot(p1, p2, layout = (2, 1))"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.4",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.4"
  },
  "widgets": {
   "application/vnd.jupyter.widget-state+json": {
    "state": {},
    "version_major": 2,
    "version_minor": 0
   }
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
